# This file is part of ICU4X. For terms of use, please see the file
# called LICENSE at the top level of the ICU4X source tree
# (online at: https://github.com/unicode-org/icu4x/blob/main/LICENSE ).

if (target_os == "") {
  target_os = host_os
}
if (target_cpu == "") {
  if (target_os == "wasi") {
    target_cpu = "wasm32"
  } else if (target_os == "android") {
    target_cpu = "arm"
  } else {
    target_cpu = host_cpu
  }
}
if (current_cpu == "") {
  current_cpu = target_cpu
}
if (current_os == "") {
  current_os = target_os
}

_configs = [ "//ffi/gn/build:rust_defaults" ]

set_defaults("executable") {
  configs = _configs
}
set_defaults("rust_library") {
  configs = _configs
}

# ==============================================================================
# TOOLCHAIN SETUP
# ==============================================================================
#
# Here we set the default toolchain, as well as the variable host_toolchain
# which will identify the toolchain corresponding to the local system when
# doing cross-compiles. When not cross-compiling, this will be the same as the
# default toolchain.

host_toolchain = "//ffi/gn/build/rust_host"

if (target_cpu == host_cpu && target_os == host_os) {
  set_default_toolchain("//ffi/gn/build/rust_host")
} else if (current_cpu == "wasm32" && current_os == "wasi") {
  set_default_toolchain("//ffi/gn/build/rust_wasi")
} else {
  assert(false, "Don't know how to build for target $target_cpu / $target_os")
}
